-- Load the ratings data into a Pig relation with specified schema
ratings = LOAD '/user/maria_dev/pig/ratings.data' AS (userID:int, movieID:int, rating:int, ratingTime:int);
-- Load the metadata from a stored file with a schema for movies
metadata = LOAD '/user/maria_dev/pig/movies.item' USING PigStorage('|')
    AS (movieID:int, movieTitle:chararray, releaseDate:chararray, videoRelease:chararray, imdbLink:chararray);
-- Transform the metadata to include a Unix time representation of the release date(new column)
nameLookup = FOREACH metadata GENERATE movieID, movieTitle,
    ToUnixTime(ToDate(releaseDate, 'dd-MM-yyyy')) AS releaseTime;
-- Group the ratings by movieID to prepare for aggregation
ratingsByMovie = GROUP ratings BY movieID;
-- Generate average ratings per movie
avgRatings = FOREACH ratingsByMovie GENERATE group as movieID, AVG(ratings.rating) as avgRating;
-- Filter to include only movies with an average rating of 5.0
fiveStarMovies = FILTER avgRatings BY avgRating > 4.0;
-- Join the five-star movies with their metadata
fiveStarsWithData = JOIN fiveStarMovies BY movieID, nameLookup BY movieID;
-- Order the five-star movies by their release time to find the oldest
oldestFiveStarMovies = ORDER fiveStarsWithData BY nameLookup::releaseTime;
-- Output the list of the oldest five-star movies
DUMP oldestFiveStarMovies;
